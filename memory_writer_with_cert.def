MEMORY {
	CERT (rwxa): ORIGIN = 0xE6300400, LENGTH = 0x00003C00
	RAM  (rwxa): ORIGIN = 0xE6304000, LENGTH = 0x0002A800
	RAM2 (rwxa): ORIGIN = 0xE632E800, LENGTH = 0x00031800
}

SECTIONS
{
	.cert : {
		. = 0x00000000;
		KEEP(*(.boot_param))
		. = 0x0000008C;
		KEEP(*(.cert_offset))
		. = 0x000001D4;
		KEEP(*(.cert_addr))
		. = 0x000002E4;
		KEEP(*(.cert_size))
		. = 0x00000D54;
		KEEP(*(.cert_addr2))
		. = 0x00000E64;
		KEEP(*(.cert_size2))
	} > CERT

	.text : {
		*(.text*)
		*(.rodata*)
		. = NEXT(64);
		__RO_END__ = .;
	} > RAM

	.data : {
		__DATA_START__ = .;
		 *(.data)
		. = NEXT(64);
		__DATA_END__ = .;
	} > RAM

	__DATA_SIZE__ = SIZEOF(.data);

	.bss : {
		__BSS_START__ = .;
		 *(.bss)
		 *(COMMON)
		 . = NEXT(64);
		__BSS_END__ = .;
	} > RAM2

	stacks (NOLOAD) : ALIGN(64) {
		__STACKS_START__ = .;
		KEEP(*(writer_stack))
		__STACKS_END__ = .;
	} > RAM2

	__BSS_SIZE__ = SIZEOF(.bss);
}
